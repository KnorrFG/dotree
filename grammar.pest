WHITESPACE = _{ "\t" | " " }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE}

file = { SOI ~ NEWLINE* ~ shell_def? ~ NEWLINE* ~ (menu ~ NEWLINE*)+ ~ EOI }
menu = { "menu" ~ string? ~ symbol ~ NEWLINE* ~ OPENBR ~ menu_body ~ CLOSINGBR }
OPENBR = _{"{"}
CLOSINGBR = _{"}"}

menu_body = { (NEWLINE* ~ entry ~ NEWLINE*)+ }
entry = { keydef ~ ":" ~ (anon_command | symbol | quick_command)}
keydef = @{ (!(":" | WHITESPACE | NEWLINE) ~ ANY)* }
symbol = @{ (ASCII_ALPHANUMERIC | "_")+ }
string = { normal_string  | protected_string }

// a normal string in which you can escape a " with a \
normal_string = ${ QUOTE ~ normal_content ~ QUOTE }
normal_content = @{ ("\\\"" | !QUOTE ~ ANY)* }
QUOTE = _{ "\"" }

// a raw string, which is delimited by !""!
// where you can insert any sign to make it unique e.g.
// !x""x!. Multiple inserts are not mirrored, so this is valid:
// !ab"content"ab!, but this is not: !ab"content"ba!
protected_string = ${(sep_start ~ protected_content ~ sep_end)} 
protected_content = @{ (!("\"" ~ PEEK ~ "!") ~ ANY)* }

quick_command = {string ~ ("-" ~ string)?}

sep_start = _{ EXCL ~ PUSH((!"\"" ~ ANY)*) ~ QUOTE}
sep_end = _{ QUOTE ~ POP ~ EXCL }
EXCL = _{ "!" }

anon_command = { "cmd" ~ NEWLINE* ~ OPENBR  
			~ NEWLINE* ~ cmd_body ~ NEWLINE* ~ CLOSINGBR }
cmd_body = { cmd_settings? ~ NEWLINE* ~ vars_def? ~ NEWLINE* ~ quick_command }
vars_def = { "vars" ~ var_def ~ (DEF_SEP* ~ var_def)* ~ NEWLINE }
DEF_SEP = _{"," ~ NEWLINE*}
var_def = { symbol }
cmd_settings = { "set" ~ symbol ~ (DEF_SEP* ~ symbol)* ~ NEWLINE }

shell_def = {"shell" ~ (string|word)+ }
word = @{ (!("\"" | WHITESPACE | NEWLINE) ~ ANY)+ }
